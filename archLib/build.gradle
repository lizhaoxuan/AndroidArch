apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 9

        buildConfigField "boolean", "DEBUG_LOG", rootProject.ext.DEBUG_LOG
        buildConfigField "boolean", "DISABLE_LOG", rootProject.ext.DISABLE_LOG
        buildConfigField "String", "LOG_TAG", rootProject.ext.LOG_TAG
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-annotations:22.2.0'
}

/*
 * rules for including "lint.jar" in aar
 */
configurations {
    lintJarImport
}

dependencies {
    lintJarImport project(path: ":archLintRules", configuration: "lintJarOutput")
}

task copyLintJar(type: Copy) {
    from (configurations.lintJarImport) {
        rename {
            String fileName ->
                'lint.jar'
        }
    }
    into 'build/intermediates/lint/'
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLintJar)
}
